load("table.stlx");

run_linear_regression := procedure(fileName, types) {
    csv    := readTable(fileName, types);
    data   := csv.getData();
    number := #data;
    dmnsn  := #data[1];    
    yList  := [];
    xList  := [];
    for (i in [1 .. number]) {
        yList[i] := data[i][1];
        xList[i] := la_vector([1.0] + data[i][2..-2]);
    }
    X := la_matrix(xList);
    y := la_vector(yList);
    w := la_solve(X! * X, X! * y);
    d := X * w - y;
  
    RSS   := d * d;
    yMean := +/ yList / number;
    TSS   := +/ [(yList[i] - yMean) ** 2 : i in [1 .. number]];
    R2    := 1 - RSS / TSS;

    return R2;
};

test := procedure() {
    types := ["double", "int", "double", "double", "double", "double", "int", "string"];
    R2    := run_linear_regression("cars.csv", types);
    print("portion of explained variance : $R2$");
};

test();
