value := cachedProcedure(State, player, limit, alpha := -1, beta := 1) {
    if (finished(State)) {
        return utility(State, player);
    }
    if (limit == 0) { return 0; }
    other := arb(players() - { player });
    val   := alpha;
    for (ns in nextStates(State, player)) {
        val := max({ val, -value(ns, other, limit - 1, -beta, -val) });
        if (val >= beta) {
            return val;
        }
    }
    return val;
};
best_move := procedure(State, player, limit := 5) {
    bestVal := value(State, player, limit);
    NS      := nextStates(State, player);
    other   := arb(players() - { player });
    return [bestVal, rnd({ns : ns in NS | -value(ns,other,limit-1) == bestVal})];
};
play_game := procedure(n := 3) {
    State := startState(n);
    print(stateToString(State));
    while (!finished(State)) {
        start        := now();
        [val, State] := best_move(State, "O", 7);
        stop         := now();
        print(cacheStats(value));
        clearCache(value);
        print("For me, the game has the value $val$.  My move:");
        print(stateToString(State));
        print("computation took $(stop - start) * 0.001$ seconds");
        if (final_msg(State)) { return; }
        State := getMove(State);
        print(stateToString(State));
        if (final_msg(State)) { return; }
    }
};
